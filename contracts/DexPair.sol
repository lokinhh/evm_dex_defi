// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {IERC20} from '@openzeppelin/contracts/token/ERC20/IERC20.sol';
import {ReentrancyGuard} from '@openzeppelin/contracts/utils/ReentrancyGuard.sol';
import {DexERC20} from './DexERC20.sol';
import {DexMath} from './libraries/DexMath.sol';
import {IDexFactory} from './interfaces/IDexFactory.sol';

contract DexPair is DexERC20, ReentrancyGuard {
  uint256 public constant MINIMUM_LIQUIDITY = 10 ** 3;
  address private constant LOCKED_LIQUIDITY = address(0xdead);

  address public factory;
  address public token0;
  address public token1;

  uint112 private reserve0;
  uint112 private reserve1;
  uint32 private blockTimestampLast;

  uint256 public price0CumulativeLast;
  uint256 public price1CumulativeLast;
  uint256 public kLast;

  event Mint(address indexed sender, uint256 amount0, uint256 amount1);
  event Burn(address indexed sender, uint256 amount0, uint256 amount1, address indexed to);
  event Swap(
    address indexed sender,
    uint256 amount0In,
    uint256 amount1In,
    uint256 amount0Out,
    uint256 amount1Out,
    address indexed to
  );
  event Sync(uint112 reserve0, uint112 reserve1);

  constructor() DexERC20('ChainDex LP', 'CDLP') {
    factory = msg.sender;
  }

  function initialize(address _token0, address _token1) external {
    require(msg.sender == factory, 'DexPair: FORBIDDEN');
    require(token0 == address(0), 'DexPair: INITIALIZED');
    token0 = _token0;
    token1 = _token1;
  }

  function getReserves() public view returns (uint112 _reserve0, uint112 _reserve1, uint32 _blockTimestampLast) {
    _reserve0 = reserve0;
    _reserve1 = reserve1;
    _blockTimestampLast = blockTimestampLast;
  }

  function _update(uint256 balance0, uint256 balance1, uint112 _reserve0, uint112 _reserve1) private {
    require(balance0 <= type(uint112).max && balance1 <= type(uint112).max, 'DexPair: OVERFLOW');
    uint32 blockTimestamp = uint32(block.timestamp % 2 ** 32);
    uint32 timeElapsed = blockTimestamp - blockTimestampLast;
    if (timeElapsed > 0 && _reserve0 != 0 && _reserve1 != 0) {
      price0CumulativeLast += uint256(DexMath.sqrt(uint256(_reserve1) * 2 ** 112 / _reserve0)) * timeElapsed;
      price1CumulativeLast += uint256(DexMath.sqrt(uint256(_reserve0) * 2 ** 112 / _reserve1)) * timeElapsed;
    }
    reserve0 = uint112(balance0);
    reserve1 = uint112(balance1);
    blockTimestampLast = blockTimestamp;
    emit Sync(reserve0, reserve1);
  }

  function mint(address to) external nonReentrant returns (uint256 liquidity) {
  (uint112 _reserve0, uint112 _reserve1,) = getReserves();
    uint256 balance0 = IERC20(token0).balanceOf(address(this));
    uint256 balance1 = IERC20(token1).balanceOf(address(this));
    uint256 amount0 = balance0 - _reserve0;
    uint256 amount1 = balance1 - _reserve1;

    uint256 _totalSupply = totalSupply();
    if (_totalSupply == 0) {
      liquidity = DexMath.sqrt(amount0 * amount1) - MINIMUM_LIQUIDITY;
      _mint(LOCKED_LIQUIDITY, MINIMUM_LIQUIDITY);
    } else {
      liquidity = DexMath.min((amount0 * _totalSupply) / _reserve0, (amount1 * _totalSupply) / _reserve1);
    }
    require(liquidity > 0, 'DexPair: INSUFFICIENT_LIQUIDITY_MINTED');
    _mint(to, liquidity);

    _update(balance0, balance1, _reserve0, _reserve1);
    if (kLast != 0) kLast = uint256(reserve0) * reserve1;
    emit Mint(msg.sender, amount0, amount1);
  }

  function burn(address to) external nonReentrant returns (uint256 amount0, uint256 amount1) {
    (uint112 _reserve0, uint112 _reserve1,) = getReserves();
    address _token0 = token0;
    address _token1 = token1;
    uint256 balance0 = IERC20(_token0).balanceOf(address(this));
    uint256 balance1 = IERC20(_token1).balanceOf(address(this));
    uint256 liquidity = balanceOf(address(this));

    uint256 _totalSupply = totalSupply();
    amount0 = (liquidity * balance0) / _totalSupply;
    amount1 = (liquidity * balance1) / _totalSupply;
    require(amount0 > 0 && amount1 > 0, 'DexPair: INSUFFICIENT_LIQUIDITY_BURNED');
    _burn(address(this), liquidity);
    IERC20(_token0).transfer(to, amount0);
    IERC20(_token1).transfer(to, amount1);
    balance0 = IERC20(_token0).balanceOf(address(this));
    balance1 = IERC20(_token1).balanceOf(address(this));

    _update(balance0, balance1, _reserve0, _reserve1);
    if (kLast != 0) kLast = uint256(reserve0) * reserve1;
    emit Burn(msg.sender, amount0, amount1, to);
  }

  function swap(uint256 amount0Out, uint256 amount1Out, address to, bytes calldata) external nonReentrant {
    require(amount0Out > 0 || amount1Out > 0, 'DexPair: INSUFFICIENT_OUTPUT_AMOUNT');
    (uint112 _reserve0, uint112 _reserve1,) = getReserves();
    require(amount0Out < _reserve0 && amount1Out < _reserve1, 'DexPair: INSUFFICIENT_LIQUIDITY');

    address _token0 = token0;
    address _token1 = token1;
    require(to != _token0 && to != _token1, 'DexPair: INVALID_TO');

    if (amount0Out > 0) IERC20(_token0).transfer(to, amount0Out);
    if (amount1Out > 0) IERC20(_token1).transfer(to, amount1Out);

    uint256 balance0 = IERC20(_token0).balanceOf(address(this));
    uint256 balance1 = IERC20(_token1).balanceOf(address(this));

    uint256 amount0In = balance0 > _reserve0 - amount0Out ? balance0 - (_reserve0 - amount0Out) : 0;
    uint256 amount1In = balance1 > _reserve1 - amount1Out ? balance1 - (_reserve1 - amount1Out) : 0;
    require(amount0In > 0 || amount1In > 0, 'DexPair: INSUFFICIENT_INPUT_AMOUNT');

    {
      uint256 balance0Adjusted = balance0 * 1000 - amount0In * 3;
      uint256 balance1Adjusted = balance1 * 1000 - amount1In * 3;
      require(
        balance0Adjusted * balance1Adjusted >= uint256(_reserve0) * _reserve1 * (1000 ** 2),
        'DexPair: K'
      );
    }

    _update(balance0, balance1, _reserve0, _reserve1);
    emit Swap(msg.sender, amount0In, amount1In, amount0Out, amount1Out, to);
  }

  function sync() external nonReentrant {
    _update(
      IERC20(token0).balanceOf(address(this)),
      IERC20(token1).balanceOf(address(this)),
      reserve0,
      reserve1
    );
  }

  function skim(address to) external nonReentrant {
    address _token0 = token0;
    address _token1 = token1;
    IERC20(_token0).transfer(to, IERC20(_token0).balanceOf(address(this)) - reserve0);
    IERC20(_token1).transfer(to, IERC20(_token1).balanceOf(address(this)) - reserve1);
  }
}
